plugins {
    id 'org.jetbrains.kotlin.jvm'

    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.12.1'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

version '0.1.0'

applicationName = parent.name
mainClassName = "${group}.downlomatic.client.LauncherKt"

dependencies {
    implementation(project(":shared"))


    def ktor_version = '1.6.1'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"

    implementation "no.tornado:tornadofx:1.7.20"
    implementation "no.tornado:tornadofx-controlsfx:0.1.1"
    implementation 'no.tornado:tornadofx-controls:1.0.6'

    runtimeOnly 'org.openjfx:javafx-graphics:$javafx.version:win'
    runtimeOnly 'org.openjfx:javafx-graphics:$javafx.version:linux'
    runtimeOnly 'org.openjfx:javafx-graphics:$javafx.version:mac'

    implementation 'net.java.dev.jna:jna:5.8.0'
    implementation 'net.java.dev.jna:jna-platform:5.8.0'
}

javafx {
    version = "11"
    modules = ['javafx.controls', 'javafx.fxml']
}

runtime {
    additive = true
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ["java.base",
               "java.logging",
               "java.desktop",
               "java.xml",
               "java.scripting",
               "jdk.unsupported",
               "jdk.crypto.cryptoki"]

    imageZip = file("$buildDir/$applicationName-${version}.zip")

    targetPlatform("linux-x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.9.1%2B1/OpenJDK11U-jdk_x64_linux_hotspot_11.0.9.1_1.tar.gz")
    }
    targetPlatform("win-x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.9.1%2B1/OpenJDK11U-jdk_x64_windows_hotspot_11.0.9.1_1.zip")
    }
    targetPlatform("macOs-x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download/jdk-11.0.9.1%2B1/OpenJDK11U-jdk_x64_mac_hotspot_11.0.9.1_1.tar.gz")
    }

    launcher {
        noConsole = true
    }
}