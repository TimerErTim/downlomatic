plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.0' apply false
}

subprojects {
    group 'eu.timerertim'

    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"


        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }
    
    test {
        useJUnitPlatform()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = "11"
    }
}


def GROUP = "downlomatic"

task("runServer") {
    group GROUP
    description "Runs the server with development optimised arguments"

    def programArguments = ["--no-file-logging", "-v"]

    dependsOn "server:run"
    doFirst {
        project("server") {
            run {
                args = programArguments
            }
        }
    }
}

task("runClient") {
    group GROUP
    description "Runs the client with development optimised arguments connecting to the official server"

    def programArguments = ["--no-file-logging", "-v"]

    dependsOn "client:runShadow"
    doFirst {
        project("client") {
            runShadow {
                args = programArguments
            }
        }
    }
}

task("distServer") {
    group GROUP
    description "Packs the server as distributable under the build/server/ directory"

    dependsOn "server:installDist"
    doLast {
        copy {
            from "${project("server").buildDir}/install", {
                include "**"
            }
            into "$buildDir"
        }
    }
}

task("distClient") {
    group GROUP
    description "Packs the client as distributable under the build/client/ directory"

    dependsOn "client:runtimeZip"
    doLast {
        copy {
            from "${project("client").buildDir}", {
                include "downlomatic-*-*.zip"
            }
            into "$buildDir/${project("client").name}"
        }
    }
}